<?xml version="1.0" encoding="utf-8"?>
<!--
///////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010-2011 Esri. All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////
-->
<viewer:BaseWidget xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:mx="library://ns.adobe.com/flex/mx"
                   xmlns:viewer="com.esri.viewer.*"
                   xmlns:toc="com.esri.viewer.components.toc.*"
                   widgetConfigLoaded="widgetConfigLoadedHandler()">
    <fx:Script>
        <![CDATA[
            import com.esri.viewer.AppEvent;

            import mx.collections.ArrayCollection;
            import mx.collections.ArrayList;
            import mx.core.FlexGlobals;
            import mx.core.UIComponent;

            import spark.components.supportClasses.ListBase;

            private var hideOpLayersTimer:Timer = new Timer(250);
            private var hideBasemapsTimer:Timer = new Timer(250);
            private var hideSuspended:Boolean = false;

            private var expandLayerItems:Boolean;
            private var basemapSelectionComponent:ListBase;
            private var previousConfigBasemapsTotal:int;

            [Bindable]
            private var shouldShowOpLayers:Boolean;
            [Bindable]
            private var shouldShowBasemaps:Boolean;

            [Bindable]
            private var basemapList:ArrayList;

            [Bindable]
            private var opLayerListButtonLabel:String;
            [Bindable]
            private var basemapsLabel:String;

            private function widgetConfigLoadedHandler():void
            {
                var showThumbnails:Boolean;

                if (configXML)
                {
                    opLayerListButtonLabel = configXML.labels.layerlistlabel || getDefaultString("layerListLabel");
                    basemapsLabel = configXML.labels.basemapslabel || getDefaultString("basemap");
                    expandLayerItems = (configXML.expandlayeritems == "true");

                    var needToInitTOC:Boolean = (!configXML.layerlist.@visible[0] || configXML.layerlist.@visible[0] == "true");
                    if (needToInitTOC)
                    {
                        AppEvent.dispatch(AppEvent.DATA_OPT_LAYERS, null, initTOCIfHasOpLayers);
                    }
                    showThumbnails = (configXML.showthumbnails[0] != "false");
                }

                initBasemaps(showThumbnails);
            }

            private function initBasemaps(showThumbnails:Boolean):void
            {
                initBasemapList();
                if (basemapList.length <= 1)
                {
                    currentState = "noBasemaps";
                }
                else
                {
                    currentState = showThumbnails ? "thumbnailOptions" : "buttonOptions";
                    initBasemapSelectionComponent(showThumbnails);
                }
            }

            private function initBasemapSelectionComponent(showThumbnails:Boolean):void
            {
                basemapSelectionComponent = showThumbnails ? basemapGallery : basemapsButtonBar;
                preselectTopmostVisibleBasemap();
                hideBasemapsTimer.addEventListener(TimerEvent.TIMER, hideBasemapsTimer_timerHandler, false, 0, true);
            }

            private function initTOCIfHasOpLayers(event:AppEvent):void
            {
                var opLayersList:Array = event.data as Array;
                if (opLayersList.length > 0)
                {
                    initTOC(expandLayerItems);
                    opLayersButton.visible = true;
                    hideOpLayersTimer.addEventListener(TimerEvent.TIMER, hideOpLayersTimer_timerHandler, false, 0, true);
                    AppEvent.addListener(AppEvent.SUSPEND_TOC_HIDE_TIMER, suspendHideListener);
                }
            }

            private function initBasemapList():void
            {
                basemapList = new ArrayList();
                var labels:Array = [];
                for each (var basemap:Object in configData.basemaps)
                {
                    var label:String = basemap.label;
                    if (labels.indexOf(label) == -1) // avoid duplicates using the labels
                    {
                        labels.push(label);
                        var basemapItem:Basemap = new Basemap(basemap.id, label, basemap.icon, basemap.visible);
                        basemapList.addItem(basemapItem);
                    }
                }
                previousConfigBasemapsTotal = configData.basemaps.length;
            }

            private function setWidgetState(showThumbnails:Boolean):void
            {
                if (basemapList.length <= 1)
                {
                    currentState = "noBasemaps";
                }
                else
                {
                    currentState = showThumbnails ? "thumbnailOptions" : "buttonOptions";
                }
            }

            private function preselectTopmostVisibleBasemap():void
            {
                var totalBasemaps:Array = basemapList.toArray().reverse();
                for each (var basemap:Basemap in totalBasemaps)
                {
                    if (basemap.visible)
                    {
                        basemapSelectionComponent.selectedItem = basemap;
                        break;
                    }
                }
            }

            private function hideOpLayersTimer_timerHandler(event:TimerEvent):void
            {
                if (!hideSuspended)
                {
                    AppEvent.dispatch(AppEvent.TOC_HIDDEN);
                    shouldShowOpLayers = false;
                }
            }

            private function hideBasemapsTimer_timerHandler(event:TimerEvent):void
            {
                shouldShowBasemaps = false;
            }

            private function suspendHideListener(event:AppEvent):void
            {
                hideSuspended = (event.data != null);
            }

            private function initTOC(expandLayerItems:Boolean = false):void
            {
                toc.map = map;
                toc.isMapServiceOnly = false; //gotta get this from the config file
                toc.excludeLayers = getExcludeLayers();
                toc.basemapLayers = getBasemapLayers();
                toc.excludeGraphicsLayers = true;
                if (expandLayerItems)
                {
                    toc.expandLayerItems();
                }
            }

            private function getExcludeLayers():ArrayCollection
            {
                var excludeLayers:ArrayCollection = getBasemapLayers();

                if (configXML)
                {
                    // exclude these layers
                    var layers:XMLList = configXML.layerlist.excludelayer as XMLList;
                    for (var j:int = 0; j < layers.length(); j++)
                    {
                        excludeLayers.addItem(layers[j].toString());
                    }
                }

                return excludeLayers;
            }

            private function getBasemapLayers():ArrayCollection
            {
                var result:ArrayCollection = new ArrayCollection();

                if (configData && configData.basemaps)
                {
                    for (var i:int = 0; i < configData.basemaps.length; i++)
                    {
                        result.addItem(configData.basemaps[i].label);
                    }
                }

                return result;
            }

            private function startHideTimer():void
            {
                hideOpLayersTimer.reset();
                hideOpLayersTimer.start();
            }

            private function opLayerListButton_rollOverHandler(event:MouseEvent):void
            {
                shouldShowBasemaps = false;
                shouldShowOpLayers = true;
                hideOpLayersTimer.reset();
                updateContainerPosition(opLayersButton, opLayersContainer);
            }

            private function updateContainerPosition(anchorComponent:DisplayObject, container:UIComponent):void
            {
                var containerGlobalBottomRightPoint:Point = anchorComponent.localToGlobal(new Point(container.width, container.height));
                positionContainerHorizontally(containerGlobalBottomRightPoint.x, anchorComponent, container);
                positionContainerVertically(containerGlobalBottomRightPoint.y, anchorComponent, container);
            }

            private function positionContainerHorizontally(x:Number, anchorComponent:DisplayObject, container:UIComponent):void
            {
                var appWidth:Number = FlexGlobals.topLevelApplication.width;
                if (x > appWidth)
                {
                    container.x = anchorComponent.x - container.width + anchorComponent.width;
                }
                else
                {
                    container.x = anchorComponent.x;
                }
            }

            private function positionContainerVertically(y:Number, anchorComponent:DisplayObject, container:UIComponent):void
            {
                var appHeight:Number = FlexGlobals.topLevelApplication.height;
                if (y > appHeight)
                {
                    container.y = anchorComponent.y + 1 - container.height;
                }
                else
                {
                    container.y = anchorComponent.height - 1;
                }
            }

            private function dispatchChangeBasemapEvent():void
            {
                AppEvent.dispatch(AppEvent.BASEMAP_SWITCH, (basemapSelectionComponent.selectedItem as Basemap).id);
            }

            protected function opLayersContainer_clickHandler(event:MouseEvent):void
            {
                AppEvent.dispatch(AppEvent.TOC_HIDDEN); // always hide the toc layer menu
            }

            private function startBasemapsHideTimer():void
            {
                hideBasemapsTimer.reset();
                hideBasemapsTimer.start();
            }

            private function basemapsButton_rollOverHandler(event:MouseEvent):void
            {
                var haveBasemapsChanged:Boolean = (previousConfigBasemapsTotal != configData.basemaps.length);
                if (haveBasemapsChanged)
                {
                    initBasemapList();
                }

                shouldShowOpLayers = false;
                shouldShowBasemaps = true;
                hideBasemapsTimer.reset();
                updateContainerPosition(basemapsButton, basemapsContainer);
            }

            private function basemapGallery_clickHandler(event:MouseEvent):void
            {
                var basemapItemRenderer:BasemapItemRenderer = event.target as BasemapItemRenderer;
                if (basemapItemRenderer)
                {
                    shouldShowBasemaps = false;
                }
            }
        ]]>
    </fx:Script>

    <viewer:states>
        <s:State name="noBasemaps"/>
        <s:State name="thumbnailOptions" stateGroups="normal"/>
        <s:State name="buttonOptions" stateGroups="normal"/>
    </viewer:states>

    <s:HGroup id="mapLayersContainer" gap="9">
        <s:Button id="opLayersButton"
                  height="100%"
                  label="{opLayerListButtonLabel}"
                  rollOut="startHideTimer()"
                  rollOver="opLayerListButton_rollOverHandler(event)"
                  visible="false"/>

        <s:ButtonBar id="basemapsButtonBar"
                     change="dispatchChangeBasemapEvent()"
                     dataProvider="{basemapList}"
                     includeIn="buttonOptions"
                     labelField="label"
                     requireSelection="true"/>

        <s:Button id="basemapsButton"
                  includeIn="thumbnailOptions"
                  label="{basemapsLabel}"
                  rollOut="startBasemapsHideTimer()"
                  rollOver="basemapsButton_rollOverHandler(event)"
                  skinClass="com.esri.viewer.skins.BasemapsButtonSkin"/>
    </s:HGroup>

    <s:BorderContainer id="opLayersContainer"
                       width="265" height="180"
                       backgroundColor="{getStyle('contentBackgroundColor')}"
                       click="opLayersContainer_clickHandler(event)"
                       includeInLayout="false"
                       rollOut="startHideTimer()"
                       rollOver="hideOpLayersTimer.reset()"
                       visible="{shouldShowOpLayers}">
        <toc:TOC id="toc"
                 left="2" right="2" top="3" bottom="3"/>
    </s:BorderContainer>

    <s:BorderContainer id="basemapsContainer"
                       backgroundColor="{getStyle('contentBackgroundColor')}"
                       includeIn="thumbnailOptions"
                       includeInLayout="false"
                       rollOut="startBasemapsHideTimer()"
                       rollOver="hideBasemapsTimer.reset()"
                       visible="{shouldShowBasemaps}">
        <s:List id="basemapGallery"
                borderVisible="false"
                change="dispatchChangeBasemapEvent()"
                click="basemapGallery_clickHandler(event)"
                dataProvider="{basemapList}"
                itemRenderer="widgets.MapSwitcher.BasemapItemRenderer"
                maxHeight="362"
                requireSelection="true">
            <s:layout>
                <s:TileLayout paddingBottom="10"
                              paddingLeft="10"
                              paddingRight="10"
                              paddingTop="10"
                              requestedColumnCount="{(basemapList.length > 2) ? 3 : 2}"/>
            </s:layout>
        </s:List>
    </s:BorderContainer>
</viewer:BaseWidget>
